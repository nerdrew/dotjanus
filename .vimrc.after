if has("gui_running")
  set guioptions-=T    " hide toolbar
  set guioptions-=r    " hide scrollbars on right
  set guioptions-=L    " hide scrollbars on left
endif

set guifont=Menlo:h11
set showmatch       " show matching parentheses
set hidden          " handle multiple buffers better
set wildmenu        " better tab completion for files
set wildmode=list:longest
set wildignore=*.swp,*.bak,*.pyc,*.class,*.png,*.o,*.jpg
set history=1000
set ruler
set backspace=indent,eol,start
set copyindent
"set listchars=tab:>\ ,trail:路,eol:$,nbsp:路,extends:>,precedes:<
set listchars=tab:>\ ,trail:路,nbsp:路,extends:>,precedes:<
set noswapfile
set ssop-=options
set ssop-=folds

set statusline+=%{SyntasticStatuslineFlag()}

if has('balloon_eval')
  set noballooneval
  set balloondelay=100000
  autocmd FileType ruby setlocal balloonexpr=
endif

if has('autocmd')
  autocmd filetype python set expandtab sw=4 ts=4 sts=4
  autocmd filetype c set sw=4 ts=8 nolist
  autocmd FileType go autocmd BufWritePre <buffer> Fmt

  " Show trailing whitepace and spaces before a tab:
  "autocmd Syntax * syn match Error /\s\+$\| \+\ze\t/

  " eruby doesn't correctly indent javascript w/o this
  "autocmd BufRead,BufNewFile *.html.erb set filetype=javascript
  "autocmd BufRead,BufNewFile *.html.erb set filetype=eruby.html
  "autocmd BufRead,BufNewFile *.js.erb set filetype=eruby.javascript

  autocmd BufRead,BufNewFile *.as set filetype=actionscript

  " Remove trailing whitespace on save
  autocmd BufWritePre *.{rb,erb,h,m,haml,js,html,coffee,json} let _s=@/ | exe "normal! msHmt" | keepj %s/\s\+$//e | let @/=_s | nohl | exe "normal! 'tzt`s"
endif

" Call yankstack BEFORE my keyboard remappings
call yankstack#setup()
"""""""""""""""""""""""""""""""""
" Keyboard mappings
"""""""""""""""""""""""""""""""""
nnoremap / /\v
vnoremap / /\v
nnoremap Y y$
nnoremap <C-e> 3<C-e>
nnoremap <C-y> 3<C-y>
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
cnoremap <C-A> <Home>
cnoremap <C-F> <Right>
cnoremap <C-B> <Left>
cnoremap <Esc>b <S-Left>
cnoremap <Esc>f <S-Right>
"noremap <D-t> :tabnew<CR>

unmap <leader>fc
unmap <leader>fef
unmap <leader>tw
unmap <leader>b
noremap <unique> <leader>w :set wrap!<CR>:set wrap?<CR>
noremap <unique> <leader>g :GundoToggle<CR>
noremap <leader><space> :nohls<cr>
noremap <leader>l :set list!<CR>:set list?<cr>
noremap <leader>p <Plug>yankstack_substitute_older_paste
noremap <leader>P <Plug>yankstack_substitute_newer_paste
noremap <Leader>f :CommandT<CR>
noremap <Leader>bb :CommandTMRU<CR>
noremap <leader>t :TagbarToggle<CR>
noremap <leader>gn <ESC>/\v^[<=>\|]{7}( .*\|$)<CR>
"map <leader>d :BuffergatorToggle<CR>
noremap <unique> <Leader>cr :RunRspecCurrentFileConque<CR>
noremap <unique> <Leader>cR :RunRspecCurrentLineConque<CR>
noremap <unique> <Leader>rc :CloseSingleConque<CR>
noremap <C-@> @@


" Plugin options
let g:CommandTMatchWindowReverse=1
let g:CommandTWildIgnore=&wildignore . ",**/bower_components/*,**/node_modules/*,**/packages/*"
"let g:buffergator_viewport_split_policy="B"
"let g:buffergator_split_size=0
"let g:buffergator_suppress_keymaps=1

let g:syntastic_quiet_warnings=1
let g:syntastic_ruby_exec="default-ruby"
let g:syntastic_html_tidy_ignore_errors=['proprietary attribute "ng-', 'is not recognized!']
"let g:syntastic_mode_map = { 'mode': 'active', 'active_filetypes': [], 'passive_filetypes': ['c'] }

" disable ruby balloon eval
let g:ruby_enable_balloons=0

let g:clang_library_path="/Library/Developer/CommandLineTools/usr/lib"

let g:vroom_use_binstubs=1
let g:SuperTabDefaultCompletionType = "context"

" From http://vim.wikia.com/wiki/Capture_ex_command_output
" Captures ex command and puts it in a new tab
function! TabMessage(cmd)
  redir => message
  silent execute a:cmd
  redir END
  tabnew
  silent put=message
  set nomodified
endfunction
command! -nargs=+ -complete=command TabMessage call TabMessage(<q-args>)


" Linters
function! XMLlint(line1, line2)
  execute a:line1.",".a:line2." !xmllint --format --recover -"
endfunction
command! -range=% -complete=command XMLlint call XMLlint(<line1>, <line2>)

function! JSONlint(line1, line2)
  execute a:line1.",".a:line2." !json_reformat"
endfunction
command! -range=% -complete=command JSONlint call JSONlint(<line1>, <line2>)


" Set ts sts sw = num
function! Tabs(num)
  let &tabstop = a:num
  let &shiftwidth = a:num
  let &softtabstop = a:num
endfunction
command! -nargs=1 -complete=command Tabs call Tabs(<args>)

function! LargeFile()
  filetype off
  set filetype=text
  set noincsearch
endfunction
command! -complete=command LargeFile call LargeFile()

function! LargeFileOff()
  filetype on
  filetype detect
  set incsearch
endfunction
command! -complete=command LargeFileOff call LargeFileOff()

function! SynStack()
  let s:syn_stack = ''
  for id in synstack(line("."), col("."))
    let s:syn_stack = s:syn_stack . ' > ' . synIDattr(id, "name")
  endfor
  echo s:syn_stack
  return s:syn_stack
endfunction

function! ShowSynStack()
  let g:old_statusline = &statusline
  let g:old_laststatus = &laststatus
  set statusline+=%{SynStack()}
  set laststatus=2
endfunction
command! -complete=command ShowSynStack call ShowSynStack()

function! HideSynStack()
  let &statusline=g:old_statusline
  let &laststatus=g:old_laststatus
endfunction
command! -complete=command HideSynStack call HideSynStack()

function! SendToCommand(UserCommand) range
  let SelectedLines = getline(a:firstline,a:lastline)
  " Convert to a single string suitable for passing to the command
  let ScriptInput = join(SelectedLines, "\n") . "\n"
  " Run the command
  echo system(a:UserCommand, ScriptInput)
endfunction
command! -complete=command -range -nargs=1 SendToCommand <line1>,<line2>call SendToCommand(<q-args>)

fu! RunCommand() range
  let RunCommandCursorPos = getpos(".")
  let SelectedLines = getline(a:firstline,a:lastline)
  " Convert to a single string suitable for passing to the command
  let ScriptInput = join(SelectedLines, " ") . "\n"
  echo system(ScriptInput)
  call setpos(".", RunCommandCursorPos)
endfu
command! -complete=command -range RunCommand <line1>,<line2>call RunCommand()
map <unique> <leader>! :RunCommand<CR>

function! StripTrailingWhitespace(line1, line2)
  let _s=@/ | exe "normal! msHmt" | exe 'keepj '.a:line1.",".a:line2.'s/\s\+$//e' | let @/=_s | nohl | exe "normal! 'tzt`s"
endfunction
command! -range=% -complete=command StripTrailingWhitespace call StripTrailingWhitespace(<line1>, <line2>)
